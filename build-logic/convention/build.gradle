plugins {
    id 'java-gradle-plugin'
}

def properties = new Properties()
rootProject.file('../config.properties').withInputStream { properties.load(it) }

java {
    sourceCompatibility = properties.getProperty("javaVersion")
    targetCompatibility = properties.getProperty("javaVersion")
}

dependencies {
    compileOnly(libs.android.gradle.plugin)
    compileOnly(libs.android.tools.common)
}

tasks.named("validatePlugins", ValidatePlugins).configure {
    enableStricterValidation = true
    failOnWarning = true
}

gradlePlugin {
    plugins {
        def prefix = "local"
        register("androidApplication") {
            id = "${prefix}.android.application"
            implementationClass = "AndroidApplicationConventionPlugin"
        }
        register("androidLibrary") {
            id = "${prefix}.android.library"
            implementationClass = "AndroidLibraryConventionPlugin"
        }
        register("javaLibrary") {
            id = "${prefix}.java.library"
            implementationClass = "JavaLibraryConventionPlugin"
        }
    }
}
